buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
plugins {
    id 'eclipse'
    id 'maven-publish'
}
// TODO move (some of) these to plugins block?
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'


version = '0.0'
group = "malte0811.${modid}"
archivesBaseName = modid

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: version_parchment

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                rubie {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name 'Jared maven'
        url 'https://maven.blamejared.com/'
    }
    maven {
        url "https://cfa2.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs 'flatdir'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_minecraft}-${version_forge}"
    implementation fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:${version_minecraft}-${version_ie}")
    implementation fg.deobf("curse.maven:rubidium-574856:3749094")

    runtimeOnly fg.deobf("curse.maven:lazydfu-460819:3544496")
    runtimeOnly fg.deobf("curse.maven:ferritecore-429235:3672146")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
    config "${modid}.mixins.json"
}

// TODO set up custom.gradle and move this to there
configurations.all {
    resolutionStrategy {
        force 'cpw.mods:securejarhandler:1.99.99'
    }
}
